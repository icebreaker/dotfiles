" options
set nospell
set encoding=utf8
set fileencoding=utf8
set selectmode=key
set nowrap
set complete-=i
set fileformats=unix,mac,dos
set nocp
set keymodel=startsel
set novisualbell
set number
set report=0
set nolazyredraw
set ttyfast
set autoindent
set cindent
set smartindent
set smartcase
set noexpandtab
set tabstop=4
set shiftwidth=4
set softtabstop=4
set cursorline
set smarttab
set showmatch
set nohlsearch
set incsearch
set nocompatible
set backspace=indent,eol,start
set diffopt+=iwhite
set hidden
set nostartofline
set shortmess=atsI
set noshowcmd
set ignorecase
set mouse-=a
set ruler
set nofsync
set clipboard=exclude:.*
set cinoptions+=g0
set foldmethod=marker
set cryptmethod=blowfish2

" netrw
let g:netrw_banner=0
let g:netrw_fastbrowse=1

" swap and undo
set history=1000
set undodir=~/.vimundo
set undolevels=1000
set undofile
set directory=~/.vimswap

" quality of life
set pastetoggle=<F3>
nnoremap <F2> :set nonumber!<CR>
nnoremap <F4> :set spell! spelllang=en_us<CR>
nnoremap <F5> :set hlsearch!<CR>

vnoremap <C-c> :w! ~/.vimswap/.buffer<CR>
nnoremap <C-p> :r ~/.vimswap/.buffer<CR>

nnoremap r :redo<CR>
nnoremap ; :

nnoremap <Leader>w :w<CR>
nnoremap <Leader>= <C-a>
nnoremap <Leader>- <C-x>

nnoremap zz ZZ
nnoremap zq ZQ

nnoremap q <nop>

augroup qol
	au!
	au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exec "normal! g'\"" | endif
	au BufWritePre * silent exec '%s/\s\+$//e'
augroup end

" syntax and colorscheme
set t_AB=^[[48;5;%dm
set t_AF=^[[38;5;%dm
set t_Co=256
set term=xterm-256color

if $TMUX =~ 'tmux'
	set term=xterm-256color
	nmap  <Home>
	imap  <Home>
	vmap  <Home>
	nmap  <End>
	imap  <End>
	vmap  <End>
endif

syntax on
syntax sync fromstart
filetype on
filetype plugin on
filetype plugin indent on
filetype indent on

let python_highlight_all=1
let c_no_curly_error=1
let coffee_no_reserved_words_error=1
let g:delimitMate_expand_cr=1

if has('gui_running')
	set guifont=IBM\ Plex\ Mono\ SemiBold\ 19
	set guicursor+=a:blinkon0
	set guioptions=i
endif

let g:html_font='IBM Plex Mono Medium'

if has('gui_running')
	colorscheme gummybears
else
	set background=dark
	colorscheme monoplex
endif

" color column and basic distraction free writing for markdown
hi ColorColumn ctermbg=124 ctermfg=NONE cterm=BOLD
call matchadd('ColorColumn', '\%120v', 100)

augroup dfw
	au!
	au BufRead,BufNewFile *.md set tw=80 nonumber
augroup end

" status line
function! GitStatus()
	let s = fugitive#head()

	if empty(s)
		return s
	else
		return '  ' . s . ' |'
	endif
endfunction

set laststatus=2
set statusline =%{GitStatus()}
set statusline+=\ %F
set statusline+=%=
set statusline+=%{''.(&fenc!=''?&fenc:&enc).''}
set statusline+=\ \|
set statusline+=\ %{&ff}
set statusline+=\ \|
set statusline+=\ %04v:%04l\ \|\ %04L
set statusline+=%{'\ '}

" word and code completion
set completeopt=menu,menuone
set pumheight=10

function! TabOrComplete()
	if col('.') > 1 && strpart(getline('.'), col('.')-2, 3) =~ '^\w'
		return "\<C-N>"
	else
		return "\<Tab>"
	endif
endfunction
inoremap <Tab> <C-R>=TabOrComplete()<CR>
inoremap <C-@> <C-X><C-U>

" file navigation
let src = substitute(substitute(expand('%:p:h'), '\/src.*$', '', '') . '/src', ' ', '\\ ', 'g')
exec 'set path+=' . src
exec 'set path+=/usr/include/SDL2'
exec 'set path+=/usr/include'

let src = substitute(substitute(expand('%:p:h'), '\/data.*$', '', '') . '/data', ' ', '\\ ', 'g')
exec 'set path+=' . src

let wildignore=[
	\'.svn',
	\'.git',
	\'.hg',
	\'bin',
	\'build',
	\'src_data',
	\'data/gfx',
	\'data/sfx',
	\'data/def',
	\'data/assets',
	\'data/textures',
	\'data/sprites',
	\'data/audio',
	\'data/sounds',
	\'data/music',
	\'data/maps',
	\'data/levels',
	\'data/worlds',
	\'data/scenes',
	\'data/fonts',
	\'resources',
	\'src/vendor'
\]
exec 'set wildignore+=' . join(wildignore, '/*,')
set wildmenu

let g:fastopen_filter_cmd = 'grep -Ev ' . "'" . join(wildignore, '/|') . "/'"
nnoremap <C-l> :call fastopen#show('argedit')<CR>

function! NextFile()
	if bufname('%') == ''
		return
	endif

	silent! exec 'try | n | catch | try | rew | catch | | endtry | endtry'
endfunction

function! PrevFile()
	if bufname('%') == ''
		return
	endif

	silent! exec 'try | prev | catch | try | la | catch | | endtry | endtry'
endfunction

function! CloseFile()
	if &modified && confirm('File modified. Are you sure?', "&Yes\n&No") == 2
		return
	endif

	try
		silent argdel %
		if len(argv()) == 0
			silent q!
		else
			call PrevFile()
		endif
	catch
		if winnr('$') > 1
			silent q!
		else
			if bufname('%') == ''
				silent q!
			else
				silent bd!
			endif
		endif
	endtry
endfunction

noremap <Leader>] :call NextFile()<CR>
noremap <Leader>[ :call PrevFile()<CR>
noremap <Leader>q :call CloseFile()<CR>

" rubocop
let g:vimrubocop_keymap = 0
nnoremap <Leader>c :RuboCop<CR>

" building and project managament
set errorformat^=%-GIn\ file\ included\ %.%#

let g:asynctotem_cclose_delay = 0
let g:asynctotem_cclose_on_kill = 1
let g:asynctotem_cclose_on_no_errors = 1

function! AsyncFindMakeDir(project)
	if exists('g:async_make_dir')
		return g:async_make_dir
	endif

	let makedir = ''

	if exists('b:git_dir') && !empty(b:git_dir)
		let gitmakedir = substitute(b:git_dir, '/\.git$', '', '')
		if gitmakedir != getcwd()
			let makedir = gitmakedir . ';'
		endif
	endif

	let makedir = makedir . '.;'

	let file = findfile(a:project, makedir)
	if empty(file)
		let makedir = '-'
		let g:async_make_dir = makedir
		return makedir
	endif

	let makedir = fnamemodify(file, ':p:h')
	if empty(makedir) || makedir == '.' || makedir == getcwd()
		let makedir = ''
	else
		let makedir = '-C ' . makedir
	endif

	let g:async_make_dir = makedir

	return makedir
endfunction

function! AsyncMake(action)
	let makedir = AsyncFindMakeDir('.project')

	if makedir == '-'
		echohl ErrorMsg
		echon 'No project found!'
		echohl None
		return
	endif

	call asynctotem#run(&makeprg, makedir, '-f .project', a:action)
endfunction

noremap <Leader>e :call AsyncMake('build')<CR>
noremap <Leader>r :call AsyncMake('execute')<CR>
noremap <Leader>k :call AsyncMake('clean')<CR>
noremap <Leader>g :call AsyncMake('generate')<CR>
noremap <Leader>f :call asynctotem#copen()<CR>

command! -nargs=+ -complete=shellcmd Arun call asynctotem#run(<q-args>)
command! -nargs=+ -complete=shellcmd Agrep call asynctotem#run('git --no-pager grep -n -I --untracked', <q-args>)

" auto make parent directory
function! AutoMakeParentDirectory()
	let dir = expand('%:h')

	if isdirectory(dir)
		return
	endif

	if confirm("Parent directory '" . dir . "' doesn't exist.", "&Create it\nor &No?") == 2
		return
	endif

	try
		call mkdir(dir, 'p')
	catch
		" noop
	endtry
endfunction
command! Mkdirp call AutoMakeParentDirectory()

augroup automakeparentdir
    au!
    au BufNewFile * :call AutoMakeParentDirectory()
augroup end

" wiki
command! Wiki e ~/.wiki/README.md

function! WikiJump()
	normal! ^

	let n = search('\.md)$', 'c', getline('.'))
	if n == 0
		return
	endif

	try
		normal! gf
	catch
		exec ':e %:h/<cfile>'
	endtry
endfunction

augroup wiki
	au!
	au BufNewFile,BufRead ~/.wiki/*.md silent! exec 'nnoremap <buffer> <Tab> :call WikiJump()<CR>'
augroup end

" task/todo
nnoremap <Leader>- :call gustav#add()<CR>
nnoremap <Leader>= :call gustav#toggle()<CR>

" templating
augroup templating
	au!
	au BufNewFile *.rb silent! exec '0r ~/.vim/templates/template.rb'
augroup end
nnoremap <Leader>t :. !template.sh '%:t' '%:p:h'<CR>

" file types
augroup filetypes
	au!
	au BufRead,BufNewFile * set formatoptions=tcq
	au BufRead,BufNewFile *.glsl,*.shader,*.shd set filetype=glsl
	au BufRead,BufNewFile *.CPP,*.H set filetype=cpp
	au BufRead,BufNewFile *.ASM set filetype=asm
	au BufRead,BufNewFile *.mm,*.m set filetype=objc
	au BufRead,BufNewFile *.me,*.symlink,*.bsh,*.localrc set filetype=sh
	au BufRead,BufNewFile *.as set filetype=actionscript
	au BufRead,BufNewFile Gemfile set syntax=off tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile Dockerfile,Jenkinsfile set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile Rakefile,Gemfile set filetype=ruby tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.ru set filetype=ruby tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.rb set filetype=ruby tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.cr set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.conf set tabstop=4 shiftwidth=4 softtabstop=4 expandtab
	au BufRead,BufNewFile .irbrc set filetype=ruby tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.erb set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.haml,*.hamlc set filetype=haml tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.sass set filetype=sass tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.scss set filetype=sass tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.yml,*.yaml set filetype=yaml tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile Doxterfile set filetype=yaml tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.rake,*.gemspec,*.js,*.jsx set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.feature,*.xml set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.install,*.inc set filetype=php tabstop=4 shiftwidth=4 softtabstop=4 noexpandtab noeol
	au BufRead,BufNewFile *.php set tabstop=4 shiftwidth=4 softtabstop=4 noexpandtab noeol
	au BufRead,BufNewFile *.rst set tabstop=3 shiftwidth=3 softtabstop=3 expandtab
	au BufRead,BufNewFile Cakefile set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.coffee,*.feature,*.jsx set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.ninja set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.lua,Trollfile,Buildfile,Maksufile,*.lud
		\ set filetype=lua tabstop=2 shiftwidth=2 softtabstop=2 noexpandtab noeol
	au BufRead,BufNewFile *.hx set filetype=haxe tabstop=2 shiftwidth=2 softtabstop=2 expandtab
	au BufRead,BufNewFile *cms-plugins*/*joomla*/*.php set filetype=php
		\ tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
augroup end
