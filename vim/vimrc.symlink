" options
set nospell
set encoding=utf8
set fileencoding=utf8
set selectmode=key
set nowrap
set complete-=i
set fileformats=unix,mac,dos
set nocp
set keymodel=startsel
set novisualbell
set number
set report=0
set nolazyredraw
set ttyfast
set autoindent
set cindent
set smartindent
set smartcase
set noexpandtab
set tabstop=4
set shiftwidth=4
set softtabstop=4
set cursorline
set smarttab
set showmatch
set nohlsearch
set incsearch
set nocompatible
set backspace=indent,eol,start
set diffopt+=iwhite
set hidden
set nostartofline
set shortmess=atsI
set noshowcmd
set ignorecase
set mouse-=a
set ruler
set nofsync
set clipboard=exclude:.*
set cinoptions+=g0
set foldmethod=marker
set cryptmethod=blowfish2
set ttimeoutlen=8

" netrw
let g:netrw_banner=0
let g:netrw_fastbrowse=1

" swap and undo
set history=1000
set undodir=~/.vimundo
set undolevels=1000
set undofile
set directory=~/.vimswap

" quality of life
set pastetoggle=<F3>
nnoremap <F2> :set nonumber!<CR>
nnoremap <F4> :set spell! spelllang=en_us<CR>
nnoremap <F5> :set hlsearch!<CR>

nnoremap r :redo<CR>
nnoremap ; :

nnoremap <Leader>w :w<CR>
nnoremap <Leader>a <C-a>
nnoremap <Leader>x <C-x>

nnoremap zz ZZ
nnoremap zq ZQ

nnoremap q <nop>

augroup qol
	au!
	au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exec "normal! g'\"" | endif
	au BufWritePre * silent exec '%s/\s\+$//e'
augroup end

" syntax and colorscheme
set t_AB=^[[48;5;%dm
set t_AF=^[[38;5;%dm
set t_Co=256
set term=xterm-256color

if $TMUX =~ 'tmux'
	set term=xterm-256color
	nmap  <Home>
	imap  <Home>
	vmap  <Home>
	nmap  <End>
	imap  <End>
	vmap  <End>
endif

let c_no_curly_error=1
let c_no_bracket_error=1
let python_highlight_all=1
let coffee_no_reserved_words_error=1
let g:delimitMate_expand_cr=1
let g:fugitive_no_maps = 1

syntax on
syntax sync fromstart
filetype on
filetype plugin on
filetype plugin indent on
filetype indent on

let g:html_font='JetBrains Mono NL ExtraBold'
set background=dark

if has('gui_running')
	set guifont=JetBrains\ Mono\ ExtraBold\ 17
	set guicursor+=a:blinkon0
	set guioptions=i
	set clipboard=unnamedplus
	colorscheme base16-default
	vnoremap <C-c> :w! !xclip -i -sel c<CR><CR>
	nnoremap <C-p> :r !xclip -o -sel -c<CR><CR>
else
	colorscheme monoplex
	vnoremap <C-c> :w! ~/.vimswap/.buffer<CR>
	nnoremap <C-p> :r ~/.vimswap/.buffer<CR>
endif

" color column and basic distraction free writing for markdown
hi ColorColumn ctermbg=124 ctermfg=NONE cterm=BOLD
call matchadd('ColorColumn', '\%120v', 100)

" set background color to transparent when we got a transparent background image
if $MARMOTA =~ '1' || $MARMOTA_TRANSPARENT_FORCE =~ '1' || $TMUX =~ 'tmux'
	hi Normal ctermbg=NONE
	hi NonText ctermbg=NONE
	hi DiffText ctermbg=NONE
endif

vnoremap q gq

augroup dfw
	au!
	au FileType markdown,text set nonumber wrap linebreak nolist tw=79
augroup end

" status line
function! GitStatus()
	let s = FugitiveHead()

	if empty(s)
		return s
	else
		return '  ' . s . ' |'
	endif
endfunction

set laststatus=2
set statusline =%{GitStatus()}
set statusline+=\ %F
set statusline+=%=
set statusline+=%{''.(&fenc!=''?&fenc:&enc).''}
set statusline+=\ \|
set statusline+=\ %{&ff}
set statusline+=\ \|
set statusline+=\ %04v:%04l\ \|\ %04L
set statusline+=%{'\ '}

" word and code completion
set completeopt=menu,menuone
set pumheight=10

function! TabOrComplete()
	if col('.') > 1 && strpart(getline('.'), col('.')-2, 3) =~ '^\w'
		return "\<C-N>"
	else
		return "\<Tab>"
	endif
endfunction
inoremap <Tab> <C-R>=TabOrComplete()<CR>
inoremap <C-@> <C-X><C-U>

" file navigation
let src = substitute(substitute(expand('%:p:h'), '\/src.*$', '', '') . '/src', ' ', '\\ ', 'g')
exec 'set path+=' . src
exec 'set path+=/usr/include/SDL2'
exec 'set path+=/usr/include'

let src = substitute(substitute(expand('%:p:h'), '\/data.*$', '', '') . '/data', ' ', '\\ ', 'g')
exec 'set path+=' . src

let s:ignorepath=[
	\'.venv/*',
	\'.svn/*',
	\'.git/*',
	\'.hg/*',
	\'bin/*',
	\'build/.*',
	\'tmp/*',
	\'vendor/*',
	\'src/.*/vendor/*',
	\'src_data/*',
	\'src/vendor/*',
	\'zig-cache/*',
	\'res/fonts/*',
	\'docs/*',
	\'media/*',
	\'support/build/.*/include/*',
	\'support/build/.*/gen/*'
\]
let s:ignoreext=[
	\'*.jpg',
	\'*.jpeg',
	\'*.gif',
	\'*.png',
	\'*.ico',
	\'*.icns',
	\'*.wav',
	\'*.mp3',
	\'*.ogg',
	\'*.ogv',
	\'*.webm',
	\'*.flac',
	\'*.qoi',
	\'*.md5sum',
	\'*.ttf',
	\'*.tga',
	\'*.bmp',
	\'*.pdf',
	\'*.PDF',
	\'*.raw',
	\'*.hex',
	\'.gitkeep',
	\'*.pyc'
\]
exec 'set wildignore+=' . join(s:ignorepath + s:ignoreext, ',')
set wildoptions=pum
set wildmenu

if has('win32')
	nnoremap <C-l> :argedit<SPACE>
else
	let s:ignorefilter = map(copy(s:ignorepath), '"^" . v:val') + map(copy(s:ignoreext), '"." . v:val . "\$"')
	let g:fastopen_filter_cmd = 'grep -Ev ' . "'" . join(s:ignorefilter, '|') . "'"
	nnoremap <C-l> :call fastopen#show('argedit')<CR>

	"let g:nextopen_config = {
		\'open_cmd': 'argedit',
		\'filter_pattern': join(s:ignorefilter, '|')
	\}
	"nnoremap <C-k> :call NextOpen()<CR>
endif

function! NextFile()
	if bufname('%') == ''
		return
	endif

	silent! exec 'try | n | catch | try | rew | catch | | endtry | endtry'
endfunction

function! PrevFile()
	if bufname('%') == ''
		return
	endif

	silent! exec 'try | prev | catch | try | la | catch | | endtry | endtry'
endfunction

function! CloseFile()
	if g:asynctotem_copen == 1
		call asynctotem#cclose()
		return
	endif

	if &modified && confirm('File modified. Are you sure?', "&Yes\n&No") != 1
		return
	endif

	if winnr() != winnr('$')
		silent q!
		return
	endif

	try
		silent argdel %
		if len(argv()) == 0
			silent q!
		else
			call PrevFile()
		endif
	catch
		if winnr('$') > 1
			silent q!
		else
			if bufname('%') == ''
				silent q!
			else
				silent bd!
			endif
		endif
	endtry
endfunction

noremap <Leader>] :call NextFile()<CR>
noremap <Leader>[ :call PrevFile()<CR>
noremap <Leader>q :call CloseFile()<CR>

" rubocop
let g:vimrubocop_keymap = 0
nnoremap <Leader>c :RuboCop<CR>

" error formats
set errorformat^=%-GIn\ file\ included\ %.%#
set errorformat^=%*[^:]:\ %f:%l:%*[^:]:%m "
set errorformat^=\ %#%f(%l\\\,%c):\ %m
set errorformat^=%f\(%l\)\ :\ %m

" shamelessly stolen and adapted from https://github.com/mikelue/vim-maven-plugin
function! SetupMavenErrorFormat()
	set errorformat=
		\%-G[INFO]\ %.%#,
		\%-G[WARNING]\ %.%#,
		\%-G[debug]\ %.%#
	set errorformat+=
		\[FATAL]\ Non-parseable\ POM\ %f:\ %m%\\s%\\+@%.%#line\ %l\\,\ column\ %c%.%#,
		\[%tRROR]\ Malformed\ POM\ %f:\ %m%\\s%\\+@%.%#line\ %l\\,\ column\ %c%.%#
	set errorformat+=
		\[%tRROR]\ %f:[%l\\,%c]\ %m
	set errorformat+=
		\%+E%>[ERROR]\ %.%\\+Time\ elapsed:%.%\\+<<<\ FAILURE!,
		\%+E%>[ERROR]\ %.%\\+Time\ elapsed:%.%\\+<<<\ ERROR!,
		\%+Z%\\s%#at\ %f(%\\f%\\+:%l),
		\%+C%.%#
endfunction

augroup MavenErrorFormat
	au!
	au BufNewFile,BufRead *.java :call SetupMavenErrorFormat()
augroup end

" building and project managament
let g:asynctotem_cclose_delay = 0
let g:asynctotem_jump_delay = 0
let g:asynctotem_copen_height = 6
let g:asynctotem_cclose_on_kill = 1
let g:asynctotem_cclose_on_no_errors = 1

function! AsyncGitDir()
	if exists('b:git_dir')
		return b:git_dir
	endif

	return FugitiveGitDir()
endfunction

function! AsyncGitGrepCmd()
	let cmd = 'git --no-pager grep -n -I --untracked'

	let gitdir = AsyncGitDir()
	if empty(gitdir)
		let cmd = 'grep -n -r'
		return cmd
	endif

	" FIXME: maybe shellescape this instead?
	let gitdir = substitute(substitute(gitdir, '/\.git$', '', ''), ' ', '\\ ', 'g')
	if empty(gitdir) || gitdir == '.' || gitdir == getcwd()
		return cmd
	endif

	return 'dot-git-grep ' . gitdir
endfunction

let s:DOTMAKE_FILENAME = '.dotmake'

function! AsyncMakeFindDotMakeDir()
	if exists('g:async_project_dir')
		return g:async_project_dir
	endif

	let makedir = ''

	let gitdir = AsyncGitDir()
	if !empty(gitdir)
		let gitmakedir = substitute(gitdir, '/\.git$', '', '')
		if gitmakedir != getcwd()
			let makedir = gitmakedir . ';'
		endif
	endif

	let makedir = '.;' . makedir

	" FIXME: maybe escape the makedir?
	let file = findfile(s:DOTMAKE_FILENAME, makedir)
	if empty(file)
		let g:async_project_dir = '-'
		return g:async_project_dir
	endif

	let makedir = fnamemodify(file, ':p:h')
	if empty(makedir) || makedir == '.' || makedir == getcwd()
		let makedir = ''
	endif

	let g:async_project_dir = makedir
	return g:async_project_dir
endfunction

function! AsyncMakeFindDotMake()
	if exists('g:async_project')
		return g:async_project
	endif

	let makedir = AsyncMakeFindDotMakeDir()
	if makedir == '-'
		let g:async_project = makedir
		return g:async_project
	endif

	if !empty(makedir)
		let makedir = '-C ' . makedir . ' '
	endif

	let g:async_project = makedir . '-f ' . s:DOTMAKE_FILENAME
	return g:async_project
endfunction

function! AsyncMake(action)
	let project = AsyncMakeFindDotMake()

	if project == '-'
		echohl ErrorMsg
		echon 'No ' . s:DOTMAKE_FILENAME . ' found!'
		echohl None
		return
	endif

	call asynctotem#run('dot-make', project, a:action)
endfunction

function! Asyncopen()
	call asynctotem#copen()
	cbottom
endfunction

noremap <Leader>e :call AsyncMake('build')<CR>
noremap <Leader>r :call AsyncMake('execute')<CR>
noremap <Leader>k :call AsyncMake('clean')<CR>
noremap <Leader>g :call AsyncMake('generate')<CR>
noremap <Leader>f :call Asyncopen()<CR>
noremap <Leader>s :call asynctotem#run(AsyncGitGrepCmd(), expand('<cword>'))<CR>

command! -nargs=+ -complete=shellcmd Arun call asynctotem#run(<q-args>)
command! -nargs=+ -complete=shellcmd Agrep call asynctotem#run(AsyncGitGrepCmd(), <q-args>)

" file templates - based on Damian Conway's plugin
let s:FILE_TEMPLATE_DIR = expand('~/.vim/templates')
let s:FILE_TEMPLATE_PREFIX = 'template'

function! AddTemplatesFromDir(templates, templatedir, templateprefix, filename, extension) abort
	if a:templatedir == '-'
		return
	endif

	let prefix = a:templateprefix
	if !empty(a:templatedir)
		let prefix = a:templatedir . '/' . prefix
	endif

	if !empty(a:filename) && a:filename[0] == '.' && empty(a:extension)
		call add(a:templates, prefix . a:filename)
	elseif a:filename == 'README'
		call add(a:templates, prefix . '.readme')
	elseif a:filename == 'LICENSE'
		call add(a:templates, prefix . '.license')
	elseif a:filename == 'Olenbuild'
		call add(a:templates, prefix . '.olenbuild')
	else
		call add(a:templates, prefix . '.' . a:filename . '.' . a:extension)
		call add(a:templates, prefix . '.' . a:extension)
	endif
endfunction

function! FindAndFillTemplate(file) abort
	let filepath = fnamemodify(a:file, ':h')
	let filepath = substitute(filepath, '%^data\/\|^data$\|^src\/\|^src$%', '', '')
	let filename = fnamemodify(a:file, ':t:r')
	let extension = fnamemodify(a:file, ':e')

	if empty(filepath) || filepath == '.'
		let packagepath = ''
		let importpath = ''
		let includepath = ''
	else
		let packagepath = substitute(filepath, '/', '.', 'g')
		let importpath = packagepath . '.'
		if importpath == '.'
			let importpath = ''
		endif
		let includepath = filepath . '/'
	end

	let variables = {
	\ 'FILENAME'     : filename,
	\ 'EXTENSION'    : extension,
	\ 'FILENAME_PRE' : toupper(filename),
	\ 'EXTENSION_PRE': toupper(extension),
	\ 'FILEPATH'     : filepath,
	\ 'PACKAGEPATH'  : packagepath,
	\ 'IMPORTPATH'   : importpath,
	\ 'INCLUDEPATH'  : includepath,
	\ 'YEAR'         : strftime('%Y')
	\}

	let templates = []

	call AddTemplatesFromDir(
		\ templates,
		\ AsyncMakeFindDotMakeDir(),
		\ '.' .. s:FILE_TEMPLATE_PREFIX,
		\ filename,
		\ extension
	\)
	call AddTemplatesFromDir(templates, s:FILE_TEMPLATE_DIR, s:FILE_TEMPLATE_PREFIX, filename, extension)

	for filename in templates
		if !filereadable(filename)
			continue
		end

		let template = join(readfile(filename), "\n")
		let template = substitute(template, '%\([A-Z_ ]\+\)%', { m -> get(variables, m[1], m[0]) }, 'g')

		call append(0, split(template, "\n"))
		break
	endfor
endfunction

augroup FileTemplates
    au!
    au BufNewFile * :call FindAndFillTemplate(expand('<afile>'))
augroup END

" auto make parent directory - based on Damian Conway's plugin
function! AutoMakeParentDirectory() abort
	let dir = expand('%:h')

	if isdirectory(dir)
		return
	endif

	if confirm("Parent directory '" . dir . "' doesn't exist.", "&Create it\nor &No?") != 1
		return
	endif

	try
		call mkdir(dir, 'p')
	catch
		" noop
	endtry
endfunction
command! Mkdirp call AutoMakeParentDirectory()

augroup AutoMakeParentDirectory
    au!
    au BufNewFile * :call AutoMakeParentDirectory()
augroup end

" wiki
command! Xwiki e ~/.wiki/README.md

function! WikiJump()
	normal! ^

	let n = search('\.md)$', 'c', getline('.'))
	if n == 0
		return
	endif

	try
		normal! gf
	catch
		exec ':e %:h/<cfile>'
	endtry
endfunction

function! SetupWiki()
	silent! exec 'nnoremap <buffer> <Tab> :call WikiJump()<CR>'
	silent! exec 'nnoremap <buffer> q :call CloseFile()<CR>'
endfunction

augroup Wiki
	au!
	au BufNewFile,BufRead ~/.wiki/*.md :call SetupWiki()
augroup end

function! SetupGitGud()
	silent! exec 'nnoremap <buffer> q :call CloseFile()<CR>'
endfunction

augroup GitGud
	au!
	au BufRead */.git/* :call SetupGitGud()
	au BufRead /tmp/* :call SetupGitGud()
augroup end

" task/todo
nnoremap <Leader>- :call gustav#add()<CR>
nnoremap <Leader>= :call gustav#toggle()<CR>

" clang complete on codespaces
if $USER == 'codespace'
	let g:clang_library_path = '/usr/lib/llvm-3.8/lib/libclang.so.1'
elseif $DOT_OS == 'ubuntu'
	let g:clang_library_path = '/usr/lib/llvm-12/lib/libclang.so.1'
elseif $CLANG_COMPLETE_LIBRARY_PATH =~ 'libclang.so'
	let g:clang_library_path = $CLANG_COMPLETE_LIBRARY_PATH
endif

" clang format
function! ClangFormat()
	let l:lines = 'all'
	py3file ~/.vim/bin/clang-format.py
endfunction

function! SetupClangFormat()
	silent! exec 'nnoremap <buffer> <Leader>v :call ClangFormat()<CR>'
endfunction

augroup ClangFormatGroup
	au!
	au BufRead,BufNewFile *.c,*.cc,*.cpp,*.cxx,*.inl,*.h :call SetupClangFormat()
augroup end

" debug; we do not do `packadd termdebug` on purpose here!
let g:termdebug_wide = 1
nnoremap <Leader>td :Termdebug<SPACE>
nnoremap <Leader>te :Eval<SPACE>
nnoremap <Leader>tr :Run<CR>
nnoremap <Leader>tc :Continue<CR>
nnoremap <Leader>ts :Step<CR>
nnoremap <Leader>to :Over<CR>
nnoremap <Leader>tb :Break<CR>
nnoremap <Leader>tq :Clear<CR>

" setup various file types
augroup SetupFileTypes
	au!
	au BufRead,BufNewFile *.glsl,*.shader,*.shd set filetype=glsl
	au BufRead,BufNewFile *.CPP,*.H,*.HPP set filetype=cpp
	au BufRead,BufNewFile *.ASM set filetype=asm
	au BufRead,BufNewFile *.mm,*.m set filetype=objc
	au BufRead,BufNewFile *.me,*.symlink,*.bsh,*.localrc set filetype=sh
	au BufRead,BufNewFile *.as set filetype=actionscript
	au BufRead,BufNewFile Gemfile set syntax=off tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile Dockerfile,Jenkinsfile set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile Rakefile,Gemfile set filetype=ruby tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.ru set filetype=ruby tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.rb set filetype=ruby tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.cr set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.conf set tabstop=4 shiftwidth=4 softtabstop=4 expandtab
	au BufRead,BufNewFile .irbrc set filetype=ruby tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.erb set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.haml,*.hamlc set filetype=haml tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.sass set filetype=sass tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.scss set filetype=sass tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.yml,*.yaml set filetype=yaml tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile Doxterfile set filetype=yaml tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.rake,*.gemspec,*.js,*.jsx set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.feature,*.xml set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.install,*.inc set filetype=php tabstop=4 shiftwidth=4 softtabstop=4 noexpandtab noeol
	au BufRead,BufNewFile *.php set tabstop=4 shiftwidth=4 softtabstop=4 noexpandtab noeol
	au BufRead,BufNewFile *.rst set tabstop=3 shiftwidth=3 softtabstop=3 expandtab
	au BufRead,BufNewFile Cakefile set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.coffee,*.feature,*.jsx set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.ninja set tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
	au BufRead,BufNewFile *.hx set filetype=haxe tabstop=2 shiftwidth=2 softtabstop=2 expandtab
	au BufRead,BufNewFile *cms-plugins*/*joomla*/*.php set filetype=php
		\ tabstop=2 shiftwidth=2 softtabstop=2 expandtab noeol
augroup end
